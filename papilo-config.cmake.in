if(NOT TARGET papilo)
  include(${CMAKE_CURRENT_LIST_DIR}/papilo-targets.cmake)
endif()
set(PAPILO_IMPORTED_TARGETS papilo)
set(PAPILO_FOUND 1)

include(CMakeFindDependencyMacro)

# If PAPILO was built with Quadmath then we also need it.
set(PAPILO_HAVE_FLOAT128 @PAPILO_HAVE_FLOAT128@)
if(PAPILO_HAVE_FLOAT128)
    list(APPEND CMAKE_MODULE_PATH @QUADMATH_MODULE_PATH@)
    find_dependency(Quadmath)
    if(NOT Quadmath_FOUND)
        message(WARNING "Dependency Quadmath was not found but is required for PAPILO_HAVE_FLOAT128.")
        set(PAPILO_FOUND 0)
    endif()
endif()

# If PAPILO was built with GMP then we also need it.
set(PAPILO_HAVE_GMP @PAPILO_HAVE_GMP@)
if(PAPILO_HAVE_GMP AND PAPILO_FOUND)
    if(NOT GMP_FOUND)
        find_dependency(GMP)
    endif()
    if(NOT GMP_FOUND)
        message(WARNING "Dependency GMP was not found but is required for PAPILO_HAVE_GMP.")
        set(PAPILO_FOUND 0)
    endif()
endif()

# If PAPILO was built with TBB then we also need it.
set(PAPILO_TBB @PAPILO_TBB@)
if(PAPILO_TBB AND PAPILO_FOUND)
    if(NOT TBB_FOUND)
        list(APPEND CMAKE_MODULE_PATH @TBB_MODULE_PATH@)
        find_package(TBB 2018 COMPONENTS tbb tbbmalloc REQUIRED HINTS ${CMAKE_CURRENT_LIST_DIR}/_deps/local/ ${CMAKE_CURRENT_LIST_DIR}/_deps/local/lib)
    endif()
    if(NOT TBB_FOUND)
        message(WARNING "Dependency TBB was not found using HINTS ${CMAKE_CURRENT_LIST_DIR}/_deps/local/ and ${CMAKE_CURRENT_LIST_DIR}/_deps/local/lib, but is required for PAPILO_TBB.")
        set(PAPILO_FOUND 0)
    endif()
endif()

# If PAPILO uses the standard hashmap then we also do.
set(PAPILO_USE_STANDARD_HASHMAP @PAPILO_USE_STANDARD_HASHMAP@)

# If PAPILO was built with iostreams / program_options / serialization then we also need it.
set(PAPILO_USE_BOOST_IOSTREAMS_WITH_ZLIB @PAPILO_USE_BOOST_IOSTREAMS_WITH_ZLIB@)
set(PAPILO_USE_BOOST_IOSTREAMS_WITH_BZIP2 @PAPILO_USE_BOOST_IOSTREAMS_WITH_BZIP2@)
set(PAPILO_COMMAND_LINE_AVAILABLE @PAPILO_COMMAND_LINE_AVAILABLE@)
set(PAPILO_SERIALIZATION_AVAILABLE @PAPILO_SERIALIZATION_AVAILABLE@)
set(BOOST_COMPONENTS)
if(PAPILO_USE_BOOST_IOSTREAMS_WITH_ZLIB OR PAPILO_USE_BOOST_IOSTREAMS_WITH_BZIP2)
    set(BOOST_COMPONENTS ${BOOST_COMPONENTS} iostreams)
endif()
if(PAPILO_COMMAND_LINE_AVAILABLE)
    set(BOOST_COMPONENTS ${BOOST_COMPONENTS} program_options)
endif()
if(PAPILO_SERIALIZATION_AVAILABLE)
    set(BOOST_COMPONENTS ${BOOST_COMPONENTS} serialization)
endif()
if(BOOST_COMPONENTS AND PAPILO_FOUND)
    if((NOT Boost_PROGRAM_OPTIONS_FOUND AND PAPILO_COMMAND_LINE_AVAILABLE) OR (NOT Boost_SERIALIZATION_FOUND AND PAPILO_SERIALIZATION_AVAILABLE) OR (NOT Boost_IOSTREAMS_FOUND AND (PAPILO_USE_BOOST_IOSTREAMS_WITH_ZLIB OR PAPILO_USE_BOOST_IOSTREAMS_WITH_BZIP2)))
        find_dependency(Boost @BOOST_MIN_VERSION@ COMPONENTS ${BOOST_COMPONENTS})
    endif()
    if(NOT Boost_IOSTREAMS_FOUND AND (PAPILO_USE_BOOST_IOSTREAMS_WITH_ZLIB OR PAPILO_USE_BOOST_IOSTREAMS_WITH_BZIP2))
        message(WARNING "Dependency Boost::iostreams was not found but is required for PAPILO_USE_BOOST_IOSTREAMS_WITH_ZLIB or PAPILO_USE_BOOST_IOSTREAMS_WITH_BZIP2.")
        set(PAPILO_FOUND 0)
    endif()
    if(NOT Boost_PROGRAM_OPTIONS_FOUND AND PAPILO_COMMAND_LINE_AVAILABLE)
        message(WARNING "Dependency Boost::program_options was not found but is required for PAPILO_COMMAND_LINE_AVAILABLE.")
        set(PAPILO_FOUND 0)
    endif()
    if(NOT Boost_SERIALIZATION_FOUND AND PAPILO_SERIALIZATION_AVAILABLE)
        message(WARNING "Dependency Boost::serialization was not found but is required for PAPILO_SERIALIZATION_AVAILABLE.")
        set(PAPILO_FOUND 0)
    endif()
endif()

# We also need Threads.
if(PAPILO_FOUND)
    if(NOT Threads_FOUND)
        find_dependency(Threads REQUIRED)
    endif()
    if(NOT Threads_FOUND)
        message(WARNING "Dependency Threads was not found but is required.")
        set(PAPILO_FOUND 0)
    endif()
endif()

if(@PAPILO_HAVE_LUSOL@ AND PAPILO_FOUND)
   enable_language(Fortran)
endif()

